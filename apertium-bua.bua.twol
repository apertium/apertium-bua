Alphabet
 А Б В Г Д Е Ё Ж З И Й К Л М Н О Ө П Р С Т У Ү Ф Х Һ Ц Ч Ш Ь Ы Ъ Э Ю Я
 а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х һ ц ч ш ь ы ъ э ю я
 %0 1 2 3 4 5 6 7 8 9
 %{А%}:а %{г%}:0 %{ь%}:0 %{ң%}:н %{и%}:и ! Archiphonemes
 ° %% %, %. %- %>:0 %  %+                ! punctuation and boundaries
 % 
 ;

Sets

Aleph = А Б В Г Д Е Ё Ж З И Й К Л М Н О Ө П Р С Т У Ү Ф Х Һ Ц Ч Ш Ь Ы Ъ Э Ю Я
        а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х һ ц ч ш ь ы ъ э ю я ; 

Vow =      и ү э а о ө у ы
           И Ү Э А О Ө У Ы ;

YotVow = я ё ю
         Я Ё Ю ;

FrontVow = ү э и ө
           Ү Э И Ө ;

BackVow = а о у
          а о у ;

NonVow = Б В Г Д Е Ё Ж З Й К Л М Н П Р С Т Ф Х Һ Ц Ч Ш Ь Ъ Я
         б в г д е ё ж з й к л м н п р с т ф х һ ц ч ш ь ъ я ; ! This is for surface forms only, so no archiphonemes

Cns = Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Һ Ц Ч Ш Ь Ъ
      б в г д ж з й к л м н п р с т ф х һ ц ч ш ь ъ ;



Rules

"Vowel harmony for {А}"
%{А%}:Vy <=> :Vx [ :Cns* [ :Cns | %{А%}:NonVow ] ]/[ [ :0 - [ %{А%}: | %{ь%}: ] ] | :%  ]* _ ;
             :Vx [ :Cns* :Cns :и :Cns* :Cns ]/[ [ :0 - [ %{А%}: | %{ь%}: ] ] | :%  ] _ ;
!             :Vx [ %{А%}:0 ]/[ :0 - %{А%}: ]* _ ;
!             :Vy [ %{А%}:0 ]/[ :0 - %{А%}: ]* _ ;
         except
             [ [ :э - %{А%}: ] | :и ]/:0* _ %{А%}: ;
         where Vx in ( ү э ө о а ё я у ) 
               Vy in ( э э ө о а о а а )
         matched ;

!@ а б а >:0 б {А}:а
!@ П а р и с >:0 т {А}:а
!@ х у р г у у л и >:0 д {А}:а

"Forced front vowel harmony for {А}"
%{А%}:Vy <=> :Vx [ :Cns* :Cns ]/[ [ :0 - %{А%}: ] | :%  ] _ ;
!             :Vx [ %{А%}:0 ]/[ :0 - %{А%}: ]* _ ;
!             :Vy [ %{А%}:0 ]/[ :0 - %{А%}: ]* _ ;
         except
             [ [ :э - %{А%}: ] | :и ]/:0* _ %{А%}: ;
             :Vx [ :Cns* :Cns ]/[ [ :0 - [ %{А%}: | %{ь%}: ] ] | :%  ] _ ;
         where Vx in ( у ) 
               Vy in ( э )
         matched ;

!@ ю у {ь}:0 >:0 {г}:г {А}:э {А}:э

"Deletion of first of two {А}s after a vowel"
%{А%}:0 <=> [ \[ :Vow | :YotVow ] ] :Vow %>: [ :0 - %{А%}: ]* _ %{А%}: ;
        except
            [ [ :э - %{А%}: ] | :и ]/:0* _ ;

!@ м а ш и н а >:0 {г}:0 {А}:0 {А}:а

"First of two {А}s to е after э"
%{А%}:е <=> [ :э - %{А%}: ]/[ :0 - %{А%}: ]* _ %{А%}: ;

!@ ү б г э >:0 {г}:0 {А}:е {А}:э

"Second of two {А}s to previous vowel"
%{А%}:Vx <=> %{А%}:Vx _ ;
             :Vx [ %{А%}:0 ]/[ :0 - %{А%}: ]* _ ;
         except
!             [ \[ :Vow | :YotVow ] ] :Vow %>: [ :0 - %{А%}: ]* %{А%}: _ ;
!             н а %>: :0 :0 _ ;
         where Vx in ( э ө о а )
         matched ;

!@ ү н э н >:0 {г}:0 {А}:э {А}:э
!@ х о р ё о >:0 {г}:г {А}:о {А}:о


!"Second {А}:э to э"
!%{А%}:э <=> %{А%}:э _ ;
!
!"Second {А}:а to а"
!%{А%}:а <=> %{А%}:а _ ;
!
!"Second {А}:о to о"
!%{А%}:о <=> %{А%}:о _ ;
!
!"Second {А}:ө to ө"
!%{А%}:ө <=> %{А%}:ө _ ;


"{А} to э when only previous vowels are и"
%{А%}:э <=> [ :и :Cns :Cns* :и :Cns :Cns* ]/:0* _ ;



!@ ж о р о о >:0 {г}:г {А}:о й

"{г} to г"
%{г%}:г <=> [ [ :Vow | :YotVow ] :Vow ]/[ :0 - [ %{г%}: | %{А%}: ] ]* _ [ %{А%}: ]/[ :0 - [ %{А%}: | %{г%}: ] ]* ;
            %{ң%}:н/[ :0 - [ %{г%}: | %{А%}: ] ]* _ ;

"{и} to ы"
%{и%}:ы <=> [ BackVow: [ :Cns - :й ]* ]/[ :0 - [ BackVow: ] ]* _ ;
        except
            [ :Vow :Vow ]/[ :0 - [ Vow: ] ]* _ ;  ! after long vowels
!            [ а:0 ]/[ :0 - [ а: ] ]* _ ;

"{и} deletes after й and long vowels"
%{и%}:0 <=> :й/[ :0 - [ а: ] ]* _ ;
            [ :Vow :Vow ]/[ :0 - [ Vow: ] ]* _ ;

!"regular stem-final н deletes before и

"Some vowels delete before genitive {и}"
Vx:0 <=> _ %{и%}:/[ :0 - [ а: ] ]* ;
     where Vx in ( а )
     matched ;
